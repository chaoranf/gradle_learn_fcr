apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
android {
    compileSdkVersion 26
    buildToolsVersion '26.0.3'

    defaultConfig {
        applicationId "hbyc.china.medical.view"
        minSdkVersion 16
        targetSdkVersion 19
        multiDexEnabled true
        versionCode 286
        versionName "2.8.4.0"
        manifestPlaceholders = [
                UMENG_CHANNEL_VALUE: "umeng",
                GETUI_APP_ID       : "TCgmn0UjYs9Qnp0OFKRnX4",
                GETUI_APP_KEY      : "d78CMdG13p740pGEu9wgxA",
                GETUI_APP_SECRET   : "9Qdehz8EzF8EyWkkw8kuN7",
                UMENG_APPKEY_VALUE : "54ae44ecfd98c587760002ae"
        ]

        resConfigs "zh"
        buildConfigField("boolean", "ENABLE_SANDBOX", "false")
        buildConfigField("boolean", "ENABLE_CHARLES", "false")
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
        applicationVariants.all {
            variant ->
                variant.outputs.all { output ->
                    outputFileName = "${defaultConfig.versionName}_${variant.buildType.name}_" + new Date().format("MMdd_HHmm", TimeZone.getTimeZone("GMT+08:00")) + ".apk"
                    output.assemble.doLast {
                        print("~~~flavor_name === " + variant.getFlavorName())
                    }
                    if (variant.getFlavorName().contains("cmtUpload")) {
                        output.assemble.doLast {
                            copy {
                                from output.outputFile.path
                                into "\\\\192.168.0.123\\public\\android打包\\2.8.4.0"
                            }
                        }
                        if (!variant.buildType.debuggable) {
                            output.assemble.doLast {
                                copy {
                                    from variant.mappingFile.path
                                    into "\\\\192.168.0.123\\public\\android打包\\2.8.4.0"
                                }
                            }
                        }


                    }

                }
        }
    }

    signingConfigs {
        release {
            storeFile file("medic123.keystore")
            storePassword "medic123"
            keyAlias "medic"
            keyPassword "medic123"
        }
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }

    buildTypes {
        release {
            debuggable false
            buildConfigField("boolean", "IS_DEBUG", "false")
            zipAlignEnabled true
            shrinkResources true
            minifyEnabled true
            signingConfig signingConfigs.release
        }

        debug {
            debuggable true
            buildConfigField("boolean", "IS_DEBUG", "true")
            zipAlignEnabled false
            shrinkResources false
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions("cmt", "charles")

    productFlavors {
        cmtTest {
            dimension "cmt"
            ndk {
                abiFilters "armeabi", "x86"
            }
        }
        cmtUpload {
            dimension "cmt"
            ndk {
                abiFilters "armeabi"
            }
        }

        canCharles {
            dimension "charles"
            buildConfigField("boolean", "ENABLE_CHARLES", "true")
        }

        https {
            dimension "charles"
            buildConfigField("boolean", "ENABLE_CHARLES", "false")
        }

    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
    useLibrary 'org.apache.http.legacy'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':androidSupportFramework')
    implementation 'com.google.guava:guava:18.0'

    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:support-fragment:26.1.0'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'

    annotationProcessor "org.androidannotations:androidannotations:4.4.0"
    implementation "org.androidannotations:androidannotations-api:4.4.0"
    annotationProcessor 'com.jakewharton:butterknife:7.0.0'
    implementation 'com.jakewharton:butterknife:7.0.0'
    implementation 'com.android.support:multidex:1.0.2'

    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    implementation(name: 'alisdk-feedback-2.5.0', ext: 'aar')
    implementation(name: 'securityguardaar3-5.1.81', ext: 'aar')
    implementation(name: 'pickerview', ext: 'aar')
    implementation(name: 'magicindicator', ext: 'aar')
    implementation 'com.squareup.okhttp3:okhttp:3.7.0'
    implementation 'com.squareup.okio:okio:1.12.0'
    implementation 'com.youth.banner:banner:1.4.9'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.shuyu:gsyVideoPlayer-java:2.1.3'
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.3'
}

greendao {
    schemaVersion 2
    daoPackage 'greendao.gen'
    targetGenDir 'src/main/java'
}

repositories {
    jcenter()
    flatDir {
        dirs 'libs'
    }
}
